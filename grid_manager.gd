extends Node2D


@export var grid_rows: int = 4
@export var grid_cols: int = 7
@export var card_scene: PackedScene
@export var slot_scene: PackedScene

var slot_size = Vector2(100, 150)  # todo: 아래에서 설정되기 때문에 아무값이나 넣어둠. 나중엔 제대로 된 값으로 수정필요
const SLOT_MARGIN_X = 10
const SLOT_MARGIN_Y = 50

var grid_slots = []
var START_POS = Vector2.ZERO


func _ready():
  set_slot_size_from_scene()
  create_grid()


func set_slot_size_from_scene():
  var temp_slot = slot_scene.instantiate()
  if temp_slot.has_method("get_size"):
    slot_size = temp_slot.get_size()
  temp_slot.queue_free()


func create_grid():
  grid_slots.clear()

  var total_width = grid_cols * slot_size.x + (grid_cols - 1) * SLOT_MARGIN_X
  var total_height = grid_rows * slot_size.y + (grid_rows - 1) * SLOT_MARGIN_Y
  var screen_size = get_viewport_rect().size

  START_POS = Vector2(
    (screen_size.x - total_width) / 2,
    (screen_size.y - total_height) / 2
  )

  for row in range(grid_rows):
    for col in range(grid_cols):
      var slot_pos = START_POS + Vector2(
        col * (slot_size.x + SLOT_MARGIN_X),
        row * (slot_size.y + SLOT_MARGIN_Y)
      )
      var add_card = row <= 2 and col <= 2  # todo: 아직은 하드코딩으로 카드 생성. 추후 동적 배치로 변경 필요
      var slot = create_slot(slot_pos, add_card)
      grid_slots.append(slot)



func get_card_size(card: Node2D) -> Vector2:
  for child in card.get_children():
    if child is Sprite2D:
      return child.texture.get_size()
    elif child is TextureRect:
      return child.texture.get_size()
    elif child is Control:
      return child.size
  return Vector2(100, 130)


func create_slot(pos: Vector2, add_card: bool = false) -> Node2D:
  var slot = slot_scene.instantiate()
  slot.position = pos
  add_child(slot)

  if add_card and card_scene:
    var card = card_scene.instantiate()
    card.set_grid_manager(self)
    card.position = Vector2.ZERO
    var card_size = get_card_size(card)
    
    if card_size.x > 0 and card_size.y > 0:
      var scale_x = slot_size.x / card_size.x
      var scale_y = slot_size.y / card_size.y
      card.scale = Vector2(scale_x, scale_y)
    
    slot.add_child(card)

  return slot
  
  
func move_card_to_best_slot(card: Node2D):
  var card_rect = Rect2(card.global_position - (slot_size / 2), slot_size)
  var best_slot: Node2D = null
  var max_overlap = -1.0

  for slot in grid_slots:
    var slot_rect = Rect2(slot.global_position - (slot_size / 2), slot_size)
    var overlap = card_rect.intersection(slot_rect)

    if overlap and overlap.size.length() > 0:
      var overlap_area = overlap.size.x * overlap.size.y
      var overlapping_cards = []

      for child in slot.get_children():
        if child == card:
          continue
        if child.is_in_group("cards"):
          overlapping_cards.append(child)

      var can_place = true
      
      for existing_card in overlapping_cards:
        if not card.has_method("can_overlap_with") or not card.can_overlap_with(existing_card):
          can_place = false
          break

      if can_place and overlap_area > max_overlap:
        max_overlap = overlap_area
        best_slot = slot

  if best_slot != null:
    card.global_position = best_slot.global_position
