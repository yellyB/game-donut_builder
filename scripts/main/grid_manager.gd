extends Node2D


const SLOT_MARGIN_X = 10
const SLOT_MARGIN_Y = 50

@export var grid_rows: int = 4
@export var grid_cols: int = 7
@export var card_scene_material: PackedScene
@export var card_scene_donut: PackedScene
@export var card_scene_customer: PackedScene
@export var card_scene_special: PackedScene
@export var slot_scene: PackedScene
var hud: Node = null  # Mainì—ì„œ í• ë‹¹
var slot_size = Vector2(100, 150)  # todo: ì•„ë˜ì—ì„œ ì„¤ì •ë˜ê¸° ë•Œë¬¸ì— ì•„ë¬´ê°’ì´ë‚˜ ë„£ì–´ë‘ . ë‚˜ì¤‘ì—” ì œëŒ€ë¡œ ëœ ê°’ìœ¼ë¡œ ìˆ˜ì •í•„ìš”
var grid_slots = []
var START_POS = Vector2.ZERO
var card_counter: int = 0


#region Godot's built-in functions
func _ready():
  TimerManager.time_finished.connect(_on_timer_finished)
  set_slot_size_from_scene()
#endregion


#region Public functions
## NOTE:: hudë¥¼ ì—°ê²°í•´ì£¼ëŠ” mainì˜ _ready ë³´ë‹¤ grid_managerì˜ readyê°€ ë¨¼ì € ì‹¤í–‰ë˜ì–´ë²„ë ¤ì„œ ìˆœì„œ ì œì–´ìš© ì²˜ë¦¬ë¥¼ ì¶”ê°€.
func initialize(hud_node: Node) -> void:
  hud = hud_node
  create_initial_slots()


func move_card_to_best_slot(card: Node2D):
  var card_rect = Rect2(card.global_position - (slot_size / 2), slot_size)
  var best_slot: Node2D = null
  var max_overlap = -1.0
  var current_slot = card.get_parent()

  for slot in grid_slots:
    if slot == current_slot:
      continue
    var slot_rect = Rect2(slot.global_position - (slot_size / 2), slot_size)
    var overlap = card_rect.intersection(slot_rect)

    if overlap and overlap.size.length() > 0:
      var overlap_area = overlap.size.x * overlap.size.y
      var overlapping_cards = []

      for child in slot.get_children():
        if child == card:
          continue
        if child.is_in_group("cards"):
          overlapping_cards.append(child)

      var can_place = true
      
      for existing_card in overlapping_cards:
        if not card.has_method("can_overlap_with") or not card.can_overlap_with(existing_card):
          can_place = false
          break

      if not can_place:
        print("ìŠ¬ë¡¯ì— ì¹´ë“œê°€ ìˆì–´ì„œ ëª»ë†“ìŒ:", slot.name)
        continue
        
      if can_place and overlap_area > max_overlap:
        max_overlap = overlap_area
        best_slot = slot

  if best_slot != null:
    card.get_parent().remove_child(card)
    best_slot.add_child(card)
    card.global_position = best_slot.global_position
  else:
    print("ğŸ”„ ì ì ˆí•œ ìŠ¬ë¡¯ì´ ì—†ì–´ì„œ ì œìë¦¬ ë³µê·€!")
    if current_slot:
      card.global_position = current_slot.global_position


func get_current_material_counts() -> Dictionary:
    var counts = {}
    # ëª¨ë“  MaterialTypeì„ 0ìœ¼ë¡œ ì´ˆê¸°í™”
    for type_name in Constants.MaterialType.keys():
        var type_enum = Constants.MaterialType[type_name]
        counts[type_enum] = 0
    
    # ê·¸ë¦¬ë“œë¥¼ ìˆœíšŒí•˜ë©° ì¬ë£Œ ì¹´ë“œ ê°œìˆ˜ ì¹´ìš´íŠ¸
    for slot in grid_slots:
        for child in slot.get_children():
            if child.is_in_group("cards") and child.get_card_type() == Constants.CardType.MATERIAL:
                var material_type = child.material_type
                if counts.has(material_type):
                    counts[material_type] += 1
    return counts


func craft_donut(donut_type: Constants.DonutType):
    var recipe = Constants.DONUT_DATA[donut_type]["recipe"]

    var slot = _find_empty_slot()
    if not slot:
        print("Error: No empty slot to place the crafted donut.")
        return

    var current_materials = get_current_material_counts()
    var required_materials = {}
    for material in recipe:
        required_materials[material] = required_materials.get(material, 0) + 1
    
    for material in required_materials:
        if current_materials.get(material, 0) < required_materials[material]:
            print("Error: Not enough materials to craft donut.")
            return

    consume_materials_for_recipe(recipe)

    var card = instantiate_donut_card(donut_type)
    if card:
        slot.add_child(card)
#endregion


#region Card Spawning
func spawn_special_card(card_type: Constants.SpecialCardType) -> void:
  _spawn_card(Callable(self, "instantiate_special_card").bind(card_type))


func spawn_customer_card() -> void:
  var slot = _find_empty_slot()
  if not slot:
    _game_over()
    return

  var card = instantiate_customer_card()
  if card:
    slot.add_child(card)


func spawn_material_card(material_type: Constants.MaterialType) -> void:
  _spawn_card(Callable(self, "instantiate_material_card").bind(material_type))


func spawn_donut_card(donut_type: Constants.DonutType) -> void:
  _spawn_card(Callable(self, "instantiate_donut_card").bind(donut_type))


# for test. ë‚˜ì¤‘ì— ì‚­ì œí•„ìš”
func spawn_all_donut_types() -> void:
  var donut_types = Constants.DONUT_DATA.keys()
  for donut_type in donut_types:
    spawn_donut_card(donut_type)


func _spawn_card(create_card_func: Callable):
  var slot = _find_empty_slot()
  if not slot:
    print("No more empty slots to spawn card.")
    return
  var card = create_card_func.call()
  if card:
    slot.add_child(card)


func instantiate_card_for_slot(card_type: String) -> Node2D:
  var card: Node2D
  match card_type:
    "DONUT":
      card = card_scene_donut.instantiate()
      card.set_donut_type(Constants.DonutType.MILK)
    "MATERIAL":
      card = card_scene_material.instantiate()
    _:
      return null

  if card == null:
    return null
  
  return _setup_card_properties(card)


func instantiate_special_card(card_type: Constants.SpecialCardType) -> Node2D:
  var card = card_scene_special.instantiate()
  card.initialize(card_type)
  return _setup_card_properties(card)
  

func instantiate_material_card(material_type: Constants.MaterialType) -> Node2D:
  var card = card_scene_material.instantiate()
  card.initialize(material_type)
  return _setup_card_properties(card)


func instantiate_donut_card(donut_type: Constants.DonutType) -> Node2D:
  var card = card_scene_donut.instantiate()
  card.initialize(donut_type)
  return _setup_card_properties(card)


func instantiate_customer_card() -> Node2D:
  var card = card_scene_customer.instantiate()
  card.connect("increase_money", Callable(hud, "_on_moeny_increase"))
  return _setup_card_properties(card)


func spawn_material_cards_by_grade(count: int, grade: Constants.MaterialGrade) -> void:
  # í•´ë‹¹ ë“±ê¸‰ì˜ ì¬ë£Œë“¤ë§Œ í•„í„°ë§
  var materials_of_grade = []
  for material_type in Constants.MaterialType.values():
    var material_data = Constants.MATERIAL_DATA[material_type]
    if material_data["grade"] == grade:
      materials_of_grade.append(material_type)
  
  # ëœë¤ìœ¼ë¡œ ìƒì„±í•  ì¬ë£Œë“¤ì„ ë¯¸ë¦¬ ì •í•¨
  var materials_to_spawn = []
  for i in range(count):
    if not materials_of_grade.is_empty():
      var random_material = materials_of_grade[randi() % materials_of_grade.size()]
      materials_to_spawn.append(random_material)
  
  # ì •í•´ì§„ ì¬ë£Œë“¤ë¡œ ì¹´ë“œ ìƒì„±
  for material_type in materials_to_spawn:
    var empty_slot = _find_empty_slot()
    if empty_slot:
      var card = instantiate_material_card(material_type)
      empty_slot.add_child(card)
    else:
      # ë¹ˆ ìŠ¬ë¡¯ì´ ì—†ìœ¼ë©´, ë‚¨ì€ ì¹´ë“œ ìƒì„±ì„ ì¤‘ë‹¨
      print("ìŠ¬ë¡¯ì´ ê°€ë“ ì°¨ì„œ ì¼ë¶€ ì¹´ë“œë¥¼ ìƒì„±í•˜ì§€ ëª»í–ˆìŠµë‹ˆë‹¤.")
      break


func _setup_card_properties(card: Node2D) -> Node2D:
  card.set_grid_manager(self)
  card.position = Vector2.ZERO

  var card_size = get_card_size(card)
  if card_size.x > 0 and card_size.y > 0:
    var scale_x = slot_size.x / card_size.x
    var scale_y = slot_size.y / card_size.y
    card.scale = Vector2(scale_x, scale_y)

  # for debugg - todo: ì¶”í›„ ì‚­ì œí•„ìš”
  var label = card.get_node("NumberLabel")
  if label and label is Label:
    label.text = str(card_counter)
    card_counter += 1

  return card
#endregion


#region Grid Management
func create_initial_slots():
  create_grid()


func create_grid():
  grid_slots.clear()

  var total_width = grid_cols * slot_size.x + (grid_cols - 1) * SLOT_MARGIN_X
  var total_height = grid_rows * slot_size.y + (grid_rows - 1) * SLOT_MARGIN_Y
  var screen_size = get_viewport_rect().size

  START_POS = Vector2(
    (screen_size.x - total_width) / 2,
    (screen_size.y - total_height) / 2
  )

  for row in range(grid_rows):
    for col in range(grid_cols):
      var slot_pos = START_POS + Vector2(
        col * (slot_size.x + SLOT_MARGIN_X),
        row * (slot_size.y + SLOT_MARGIN_Y)
      )
      var slot = create_slot(slot_pos)
      grid_slots.append(slot)


func create_slot(pos: Vector2) -> Node2D:
  var slot = slot_scene.instantiate()
  slot.position = pos
  add_child(slot)

  return slot
#endregion


#region Signal handlers
func _on_timer_finished():
  spawn_customer_card()


func _game_over():
  print("GAME OVER: No more empty slots!")
  get_tree().paused = true
  if hud:
    hud.show_game_over_screen()
#endregion


#region Helper functions
func _find_empty_slot() -> Node2D:
  for slot in grid_slots:
    var has_card = false
    for child in slot.get_children():
      if child.is_in_group("cards"):
        has_card = true
        break
    if not has_card:
      return slot
  return null


func consume_materials_for_recipe(recipe: Array) -> void:
    var materials_to_consume = {}
    for material in recipe:
        materials_to_consume[material] = materials_to_consume.get(material, 0) + 1

    for slot in grid_slots:
        for card in slot.get_children():
            if not card.is_in_group("cards") or card.get_card_type() != Constants.CardType.MATERIAL:
                continue

            var material_type = card.material_type
            if materials_to_consume.get(material_type, 0) > 0:
                materials_to_consume[material_type] -= 1
                card.queue_free()
                break


func set_slot_size_from_scene():
  var temp_slot = slot_scene.instantiate()
  if temp_slot.has_method("get_size"):
    slot_size = temp_slot.get_size()
  temp_slot.queue_free()


func get_card_size(card: Node2D) -> Vector2:
  for child in card.get_children():
    if child is Sprite2D:
      return child.texture.get_size()
    elif child is TextureRect:
      return child.texture.get_size()
    elif child is Control:
      return child.size
  return Vector2(100, 130)
#endregion
